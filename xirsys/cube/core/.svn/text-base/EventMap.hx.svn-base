/**
 * Copyright (c) 2011, Influxis.
 * 
 * support@influxis.com
 * 
 * All rights reserved.
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *   - Redistributions of source code must retain the above copyright
 *     notice, this list of conditions and the following disclaimer.
 *   - Redistributions in binary form must reproduce the above copyright
 *     notice, this list of conditions and the following disclaimer in the
 *     documentation and/or other materials provided with the distribution.
 *
 * THIS SOFTWARE IS PROVIDED BY INFLUXIS "AS IS" AND ANY EXPRESS OR IMPLIED 
 * WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
 * MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO
 * EVENT SHALL INFLUXIS OR THEIR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, 
 * INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES 
 * (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
 * SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
 * CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
 * LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
 * OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH
 * DAMAGE.
 * 
 * @author Lee Sylvester
 **/

package thx.cube.core;

import thx.cube.abstract.ICentralDispatcher;
import thx.cube.abstract.IEventMap;

typedef Listener<E> = {
	dispatcher : ICentralDispatcher<E>,
	type : String,
	listener : Dynamic->Void,
	eventClass : Class<E>,
	cb : E->Void
}

class EventMap<E> implements IEventMap<E> {
	public var eventDispatcher : ICentralDispatcher<E>;
	public var dispatcherListeningEnabled : Bool;
	public var listeners : Array<Listener<E>>;
	
	//---------------------------------------------------------------------
	//  Constructor
	//---------------------------------------------------------------------
	
	public function new( eventDispatcher : ICentralDispatcher<E> )
	{
		listeners = new Array<Listener<E>>();
		this.eventDispatcher = eventDispatcher;
		dispatcherListeningEnabled = true;
	}
	
	//---------------------------------------------------------------------
	//  API
	//---------------------------------------------------------------------
	
	public function mapListener( dispatcher : ICentralDispatcher<E>, type : String, listener : E->Void, ?eventClass : Class<E> = null )
	{
		if ( dispatcherListeningEnabled == false && dispatcher == eventDispatcher )
			throw new CubeError( CubeError.E_EVENTMAP_NOSNOOPING );
		//eventClass = ( eventClass != null ) ? eventClass : Class<E>;
		
		var params : Listener<E>;
		var i : Int = listeners.length;
		while ( --i > -1 )
		{
			params = listeners[i];
			if ( params.dispatcher == dispatcher
				&& params.type == type
				&& params.listener == listener
				&& params.eventClass == eventClass )
			{
				return;
			}
		}
		var me = this;
		var cb : E->Void = function( event : E )
		{
			me.routeEventToListener( event, listener, eventClass );
		};
		params = {
				dispatcher: dispatcher,
				type: type,
				listener: listener,
				eventClass: eventClass,
				cb: cb
			};
		listeners.push( params );
		dispatcher.addEventHandler( type, cb );
	}
	
	public function unmapListener( dispatcher : ICentralDispatcher<E>, type : String, listener : E->Void, ?eventClass : Class<E> = null )
	{
		var params : Listener<E>;
		var i = listeners.length;
		while ( i >= 0 )
		{
			params = listeners[i];
			if ( params.dispatcher == dispatcher
				&& params.type == type
				&& params.listener == listener
				&& params.eventClass == eventClass )
			{
				dispatcher.remove( type, params.cb );
				listeners.splice( i, 1 );
				return;
			}
			i--;
		}
	}
	
	/**
	 * Removes all listeners registered through <code>mapListener</code>
	 */
	public function unmapListeners()
	{
		var params : Listener<E>;
		var dispatcher : ICentralDispatcher<E>;
		while ( { params = listeners.pop(); params != null; } )
		{
			dispatcher = params.dispatcher;
			dispatcher.remove( params.type, params.cb );
		}
	}
	
	//---------------------------------------------------------------------
	//  Internal
	//---------------------------------------------------------------------
	
	public function routeEventToListener( event : E, listener : E->Void, originalEventClass : Class<E> )
	{
		if ( Std.is( event, originalEventClass ) )
		{
			listener( event );
		}
	}
}
